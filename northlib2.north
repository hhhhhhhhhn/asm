global write
# string: *u8, file_desc: u64 -> byte_written: u64
fn write
	# string file_desc
	1 unrot # 1 string file_desc
	swap # 1 file_desc string
	dup # 1 file_desc string string
	strlen # 1 file_desc string strlen
	syscall
end

global open_file_writing
# filename: *u8 -> file_desc: u64
fn open_file_writing
	2 swap # 2 filename
	577 # O_WRONLY | O_CREAT | O_TRUNC
	0770 # Mode
	syscall
end

global close_file
# file_desc: u64
fn close_file
	3 swap # 3 file_desc
	0 0 # padding args
	syscall
end

# n: u64 -> rounded: u64
fn round_up_to_page
	dup 0b1111_1111_1111 # n n 0b1111_1111_1111
	band 0 ne # n needs_rounding_up?
	4096 mul # n correction
	swap # correction n
	0xfffffffffffff000 band # correction round(n, 4096)
	add
end

global mmap
# pages (how many multiples of 4096 bytes): u64 -> addr: void*
fn mmap
	round_up_to_page # bytes
	9 swap # SYSCALL_MMAP bytes
	0 swap # SYSCALL_MMAP NULL bytes
	3 # PROT_READ | PROT_WRITE
	34 # MAP_PRIVATE | MAP_ANONYMOUS
	-1 # fd (ignored)
	0 # offset (ignored)
	syscall7
end

global munmap
# addr: void*, pages: u64 -> status
fn munmap
	11 unrot # SYSCALL_MUNMAP addr pages
	round_up_to_page # SYSCALL_MUNMAP addr bytes
	0 # Padding
	syscall
end
